command /addlistitem <item>:
	permission: avarizia.commands.addlistitem
	permission message: %{p::noperm}%
	trigger:
		set {_item} to arg-1
		delete {_item}'s lore
		add {_item} to {home::choosable_items::*}
		send "§a%arg-1% wurde der Liste hinzugefügt!" to player

command /removelistitem <item>:
	permission: avarizia.commands.removelistitem
	permission message: %{p::noperm}%
	trigger:
		set {_item} to arg-1
		remove {_item} from {home::choosable_items::*}
		send "§a%arg-1% wurde aus der Liste entfernt!" to player

on tab complete of "/removelistitem":
	set tab completions to {home::choosable_items::*}

on tab complete of "/addlistitem":
	set tab completions to all items

function HomeMenu(p: player) :: inventory:
	set {_g} to a chest inventory with 5 rows named "§5Homes von %{p::pc}%%{_p}%"
	loop 45 times:
		set slot loop-value - 1 of {_g} to a gray stained glass pane named "§7"
	set {_slotindex} to 10
	loop {homes::%{_p}%::*}:
		set slot {_slotindex} of {_g} to {homes::%{_p}%::%loop-index%::item} named {homes::%{_p}%::%loop-index%::clearname} with lore {homes::%{_p}%::%loop-index%::description} and "" and "%{homes::%{_p}%::%loop-index%::positionstring}%" and "%{p::c1}%ID: %{p::c2}%%loop-index%" and "" and "%{p::c4}%> %{p::c1}%Linksklick: %{p::c2}%Teleport" and "%{p::c4}%> %{p::c1}%Rechtsklick: %{p::c2}%Bearbeiten"
		set {homes::%{_p}%::%loop-index%::slotindex} to {_slotindex}
		add 1 to {_slotindex}
	set slot 44 of {_g} to a lime stained glass pane named "§aHome erstellen" with lore "§7Erstelle einen neuen Home-Punkt an deiner aktuellen Position."
	return {_g}

command /purgehomes:
	trigger:
		loop {homes::%player%::*}:
			clear {homes::%player%::%loop-index%}


function ItemPicker(p:player, h: string) :: inventory:
	set {_g} to a chest inventory with 6 rows named "§5Item Auswahl"
	loop 54 times:
		set slot loop-value - 1 of {_g} to a gray stained glass pane named "§7"
	
	set {_s} to 0
	loop {home::choosable_items::*}:
		set slot {_s} of {_g} to loop-value with lore "§7Klicke, um dieses Item für deinen Home-Punkt auszuwählen."
		add 1 to {_s}
	
	return {_g}


function HomeEditMenu(p: player, h: string) :: inventory:
	set {_g} to a chest inventory with 3 rows named "§5Home bearbeiten"
	loop 27 times:
		set slot loop-value - 1 of {_g} to a gray stained glass pane named "§7"
	
	# Löschen
	set slot 10 of {_g} to a barrier named "§cHome löschen" with lore "§7Löscht diesen Home §c§l§ndauerhaft§7!" and "" and "§7§oDieses Item §ndroppen§7§o, um den Home §c§l§odauerhaft §7§ozu löschen."
	#Postion
	set slot 11 of {_g} to a ender pearl named "§aPosition setzen" with lore "§7Setzt die Position des Homes auf deine aktuelle Position." and "" and "§7§oDieses Item §nrechtsklicken§7§o, um die Position" and "§7§odes Homes auf deine aktuelle Position zu setzen." and "§c§oAchtung: Dies überschreibt die aktuelle Position des Homes!"
	# Name
	set slot 12 of {_g} to a paper named "§aName setzen" with lore "§7Setze einen neuen Namen für diesen Home"
	# Home Info
	set slot 13 of {_g} to {homes::%{_p}%::%{_h}%::item} named "§a%{homes::%{_p}%::%{_h}%::clearname}%" with lore "§7%{homes::%{_p}%::%{_h}%::description}%"
	# Beschreibung
	set slot 14 of {_g} to a paper named "§aBeschreibung setzen" with lore "§7Setze eine neue Beschreibung für diesen Home"
	# Item
	set slot 15 of {_g} to a item frame named "§aItem setzen" with lore "§7Setze ein neues Item für diesen Home"
	# Zurück
	set slot 16 of {_g} to a red stained glass pane named "§cZurück" with lore "§7Gehe zurück zum Home-Menü"
	

	# Übergabe des Bearbeitungs-Status
	set {homes::%{_p}%::%{_h}%::editing} to true

	return {_g}


function SetHomeLocation(p: player, h: string):
	set {homes::%{_p}%::%{_h}%} to location of {_p}
	set {homes::%{_p}%::%{_h}%::positionstring} to "%{p::c1}%X: %{p::c2}%%round({_p}'s x coordinate)% %{p::c1}%Y: %{p::c2}%%round({_p}'s y coordinate)% %{p::c1}%Z: %{p::c2}%%round({_p}'s z coordinate)%"


on inventory click:
	name of event-inventory is "§5Homes von %{p::pc}%%player%"
	cancel event
	if index of event-slot is 44:
		close player's inventory
		set {chat::input::%player%} to true
		set {homes::%player%.newhome} to true
		send ""
		send ""
		send "§aBitte gib einen Namen für diesen Home in den Chat ein:" to player

	else:
		loop {homes::%player%::*}:
			if index of event-slot is {homes::%player%::%loop-index%::slotindex}:
				if event-click type is left mouse button:
					close player's inventory
					teleportPlayer(player, {homes::%player%::%loop-index%})
					stop loop
				else if event-click type is right mouse button:
					open HomeEditMenu(player, loop-index) to player
					stop loop
				else if event-click type is drop key:

on inventory click:
	name of event-inventory is "§5Home bearbeiten"
	cancel event
	
	# Home löschen
	if index of clicked slot is 10:
		if click type is drop key:
			loop {homes::%player%::*}:
				if {homes::%player%::%loop-index%::editing} is true:
					clear {homes::%player%::%loop-index%}
					open HomeMenu(player) to player
					stop loop
	
	# Neue Position setzen
	else if index of clicked slot is 11:
		if click type is rmb:
			loop {homes::%player%::*}:
				if {homes::%player%::%loop-index%::editing} is true:
					SetHomeLocation(player, loop-index)
					set {homes::%player%::%loop-index%::editing} to false
					open HomeMenu(player) to player
					stop loop 
	
	# Neuen Namen setzen
	else if index of clicked slot is 12:
		loop {homes::%player%::*}:
			if {homes::%player%::%loop-index%::editing} is true:
				set {chat::input::%player%} to true
				set {homes::%player%::%loop-index%::awaitname} to true
				set {homes::%player%::%loop-index%::editing} to false
				close player's inventory
				send ""
				send ""
				send formatted "%{p::home}% Bitte gib einen neuen Namen für diesen Home in den Chat ein. Du kannst dabei auch %{p::c3}%[%{p::c2}%<url:kommt bald>Farbcodes%{p::c3}%]<reset> %{p::c1}%nutzen!" to player
				stop loop

	# Neue Beschreibung setzen
	else if index of clicked slot is 14:
		loop {homes::%player%::*}:
			if {homes::%player%::%loop-index%::editing} is true:
				set {chat::input::%player%} to true
				set {homes::%player%::%loop-index%::awaitdesc} to true
				set {homes::%player%::%loop-index%::editing} to false
				close player's inventory
				send ""
				send ""
				send formatted "%{p::home}% Bitte gib eine neue Beschreibung für diesen Home in den Chat ein. Du kannst auch %{p::c3}%[%{p::c2}%<url:kommt bald>Farbcodes%{p::c3}%]<reset> %{p::c1}%nutzen!" to player
				stop loop
	
	# Neues Item setzen
	else if index of clicked slot is 15:
		loop {homes::%player%::*}:
			if {homes::%player%::%loop-index%::editing} is true:
				open ItemPicker(player, loop-index) to player
				
	# Zurück, Variablen zurücksetzen
	else if index of clicked slot is 16:
		loop {homes::%player%::*}:
			if {homes::%player%::%loop-index%::editing} is true:
				set {homes::%player%::%loop-index%::editing} to false
				open HomeMenu(player) to player
				stop loop

on inventory click:
	name of event-inventory is "§5Item Auswahl"
	cancel event

	loop {homes::%player%::*}:
		if {homes::%player%::%loop-index%::editing} is true:
			set {homes::%player%::%loop-index%::item} to event-item
			set {homes::%player%::%loop-index%::editing} to false
			open HomeMenu(player) to player
			stop loop	


on chat:
	if {chat::input::%player%} is true:
		cancel event
		set {chat::input::%player%} to false
		
		# Neuen Home erstellen
		if {homes::%player%.newhome} is true:
			
			set {homes::%player%.newhome} to false
			set {chat::input::%player%} to false
			
			set {_id} to message in lower kebab case
			if {homes::%player%::%{_id}%} is set:
				send formatted "%{p::home}% %{p::ec}%Du hast bereits einen Home mit diesem Namen!"
				send formatted "%{p::home}% %{p::ec}%Home-Erstellung abgebrochen."
				send formatted "%{p::home}% %{p::ec}%Bitte wähle einen anderen Namen und starte den Vorgang erneut."
				stop
			SetHomeLocation(player, {_id})
			set {homes::%player%::%{_id}%::clearname} to "§a%message%"
			set {homes::%player%::%{_id}%::item} to a grass block
			set {homes::%player%::%{_id}%::description} to "§7§oKeine Beschreibung"

			open HomeMenu(player) to player
		

		# Beschreibung ändern
		loop {homes::%player%::*}:
			if {homes::%player%::%loop-index%::awaitdesc} is true:
				set {homes::%player%::%loop-index%::description} to formatted message
				set {homes::%player%::%loop-index%::awaitdesc} to false
				send formatted "%{p::home}% %{p::bc}%Beschreibung für Punkt %{p::c3}%'%{p::c2}%%{homes::%player%::%loop-index%::clearname}%%{p::c3}%' %{p::bc}%erfolgreich geändert!"
				stop loop

		# Namen ändern
		loop {homes::%player%::*}:
			if {homes::%player%::%loop-index%::awaitname} is true:
				set {homes::%player%::%loop-index%::clearname} to formatted message
				set {homes::%player%::%loop-index%::awaitname} to false
				send formatted "%{p::home}% %{p::bc}%Der Home wurde erfolgreich zu %{p::c3}%'%{p::c2}%%message%%{p::c3}%' %{p::bc}%umbenannt."
				stop loop





